# $Id: configure.in 1617 2005-11-03 17:41:39Z laforge $
dnl Process this file with autoconf to produce a configure script.
AC_INIT(Makefile.am)
AC_CANONICAL_SYSTEM

AC_CONFIG_AUX_DIR(config)
AM_CONFIG_HEADER(config.h)

AM_INIT_AUTOMAKE(scez,1.0)

echo "Target: ${target}"
case "${target}" in
	arm-*-linux-gnu)
		echo "Target: arm-linux"
		# special stuff for Windoze NT
		# Do we need to set cross_compiling here or is it sufficient
		# to rely on AC_PROG_CC which is called later?
		cross_compiling=yes
		AR="arm-linux-ar"
		AS="arm-linux-as"
		CC="arm-linux-gcc"
		CPP="arm-linux-gcc -E"
		GCC="arm-linux-gcc"
		LD="arm-linux-ld"
		NM="arm-linux-nm"
		OBJCOPY="arm-linux-objcopy"
		RANLIB="arm-linux-ranlib"
		STRIP="arm-linux-strip"
		;;
	*)
		echo "Target: other"
		;;
esac

dnl Checks for programs.
AC_PROG_AWK
AC_PROG_CC
AC_PROG_RANLIB
AC_PROG_INSTALL
AC_PROG_LN_S
AC_PROG_MAKE_SET

dnl Enable use of libtool
#AC_LIBTOOL_DLOPEN
#AM_DISABLE_STATIC
AM_PROG_LIBTOOL

dnl Checks for libraries.
AM_CONDITIONAL(WITH_DES, false)
if test "$cross_compiling" != "yes"; then
	AC_CHECK_LIB(crypto, SSLeay_version)
	if test "$ac_cv_lib_crypto_SSLeay_version" != "yes"; then
		AC_CHECK_LIB(des, des_ecb3_encrypt)
		if test "$ac_cv_lib_des_des_ecb3_encrypt" != "yes"; then
			AC_CHECK_LIB(crypt, cryptInit)
			if test "$ac_cv_lib_crypt_cryptInit" = "yes"; then
				AM_CONDITIONAL(WITH_DES, true)
			fi
		else
			AM_CONDITIONAL(WITH_DES, true)
		fi
	else
		AM_CONDITIONAL(WITH_DES, true)
	fi
	AC_CHECK_LIB(dl, dlopen)
	#AC_CHECK_LIB(keeper, kp_dir_open)
	#AC_CHECK_LIB(sio, SIO_Open)

	bcrl=nA
	AC_CHECK_LIB(ncurses,tparm,TERMLIB=-lncurses,
	AC_CHECK_LIB(termcap,tgetent,TERMLIB=-ltermcap))
	saved_LIBS="$LIBS"
	LIBS="$LIBS $TERMLIB"
	AC_CHECK_LIB(readline,readline,
		[AC_CHECK_HEADER(readline/readline.h,
		LIBS="$saved_LIBS -lreadline $TERMLIB";bcrl=y)],
		LIBS="$saved_LIBS")
	if test "$bcrl" = "y" ; then
		echo Using the readline library.
		#AC_DEFINE(READLINE,1)
	fi

	AC_CHECK_LIB(history, using_history)
else
	AC_CHECK_LIB(dl, dlopen)
	AC_CHECK_LIB(des, des_ecb3_encrypt)
	if test "$ac_cv_lib_des_des_ecb3_encrypt" = "yes"; then
		AM_CONDITIONAL(WITH_DES, true)
	fi
fi

dnl Checks for header files.
AC_HEADER_STDC
AC_CHECK_HEADERS(dlfcn.h fcntl.h readline/history.h readline/readline.h sgtty.h sys/file.h sys/ioctl.h sys/time.h unistd.h)

dnl Checks for typedefs, structures, and compiler characteristics.
AC_C_CONST
AC_HEADER_TIME

dnl Checks for library functions.
AC_PROG_GCC_TRADITIONAL
AC_FUNC_MEMCMP
AC_FUNC_VPRINTF
AC_CHECK_FUNCS(select strdup)

dnl Checks for user selectable features
dnl keeper
AC_ARG_ENABLE(keeper, [\
  --enable-keeper         Use keeper library for configuration storage],
[
 case "${enableval}" in
  yes)
    use_keeper=true
    AC_CHECK_LIB(keeper, kp_dir_open)
    ;;
  no)
    use_keeper=false
    AC_MSG_RESULT([   keeper not used])
    ;;
  *) AC_MSG_ERROR(bad value ${enableval} for --enable-keeper)
    ;;
 esac
], [
 use_keeper=false
 AC_MSG_RESULT([   keeper not used])
])

dnl jibapi
JIBAPILIB=
JIBAPIINC=
BLUEDOTSRC=
AC_ARG_ENABLE(jibapi, [\
  --enable-jibapi=DIR     Enable JiB-API and specify location of library and\
                              header files.],
[
 case "${enableval}" in
  *)
    use_jibapi=true
	JIBAPILIB=${enableval}/jibapi.a
	JIBAPIINC=-I${enableval}
	BLUEDOTSRC=scbluedot.c
    ;;
 esac
], [
 use_jibapi=false
 AC_MSG_RESULT([   jibapi not used])
])
AC_SUBST(JIBAPILIB)
AC_SUBST(JIBAPIINC)
AC_SUBST(BLUEDOTSRC)

AC_OUTPUT(Makefile scez/Makefile scez/cards/Makefile scez/readers/Makefile sio/Makefile test/Makefile apps/Makefile doc/Makefile)

echo \
"------------------------------------------------------------------------
Configuration:

  Source code location:   ${srcdir}
  Compiler:               ${CC}
  Compiler flags:         ${CFLAGS}
  Host System Type:       ${host}
  Install path:           ${prefix}

  See config.h for further configuration information.
------------------------------------------------------------------------"

